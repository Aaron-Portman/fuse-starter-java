buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE")
    }
}

plugins {
  id 'net.ltgt.apt' version '0.13'
  id 'net.ltgt.apt-eclipse' version '0.13'
  id 'java'
  id 'maven'
  id 'org.sonarqube' version '2.6'
  id 'checkstyle'
}

// I don't know why I can't add this one in the previous plugins {} section, but it doesn't seem to work there.
apply plugin: 'org.springframework.boot'

group = 'org.galatea'
version = '0.0.1-SNAPSHOT'

description = """starter-java"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
		mavenCentral()
}

// need a 'global' exclude to prevent pulling in the logback dependency that comes
// with spring boot by default.  We've chosen to go with log4j2.
configurations.compile.exclude module : 'spring-boot-starter-logging'

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-activemq'

    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version:'2.8.10'
    compile group: 'org.springframework', name: 'spring-aop', version:'4.3.13.RELEASE'
    compile group: 'org.aspectj', name: 'aspectjweaver', version:'1.8.13'

    compile group: 'org.apache.activemq', name: 'activemq-broker', version:'5.14.5'

    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version:'1.4.0.RELEASE'
    compile group: 'io.github.openfeign', name: 'feign-gson', version:'9.5.1'

    compile group: 'com.h2database', name: 'h2', version:'1.4.196'
    compile group: 'net.sf.aspect4log', name: 'aspect4log', version:'1.0.7'
    compile group: 'com.google.collections', name: 'google-collections', version:'1.0'
    compile group: 'org.apache.commons', name: 'commons-collections4', version:'4.1'
    compile group: 'net.sf.ehcache', name: 'ehcache', version:'2.10.4'
    compile group: 'org.springframework', name: 'spring-context-support', version:'4.3.13.RELEASE'
    compile group: 'com.googlecode.junit-toolbox', name: 'junit-toolbox', version:'2.2'

  	testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
			exclude(module: 'commons-logging')
    }
    testCompile group: 'org.awaitility', name: 'awaitility', version:'2.0.0'
    testCompile group: 'pl.pragmatists', name: 'JUnitParams', version:'1.0.5'

    // lombok is only involved during the compilation phase and not needed during runtime
    compileOnly group: 'org.projectlombok', name: 'lombok', version:'1.16.18'
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version:'1.16.18'

    apt 'org.projectlombok:lombok:1.16.18' // supports annotation processing, particularly for lombok
    testApt 'org.projectlombok:lombok:1.16.18'  // our test classes use lombock too
}

checkstyle {
  toolVersion '7.6.1'
  // configFile = rootProject.file('google_checks_2.xml')
  config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
}

sonarqube {
  properties {
    property 'sonar.coverage.exclusions', '**/FuseWebRequestTraceFilter.java,**/SettlementMission.java'
  }
}


test {

    useJUnit {
        // exclude integration tests for the normal test task
        // we'll have a separate task for integration tests that gets run by jenkins,
        // but for people just doing a 'gradle build' let's avoid calling out to Cloud Foundry
        excludeCategories 'org.galatea.starter.IntegrationTestCategory'
    }

    // in order to avoid picking up the UnitTestRunner which was only intended for use in the IDE
    // we'll override the default behavior and specify which test classes to look for
    // https://docs.gradle.org/2.14/userguide/java_plugin.html#N15AAD
    include '**/*Test.class'
}
